apply plugin: 'java'
java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

// Apply application plugin for easy application start
apply plugin: 'application'

mainClassName = "uk.gov.justice.digital.pdf.Server"

version = '0.1.' + (System.getenv("CIRCLE_BUILD_NUM") ?: "SNAPSHOT")

// Apply the groovy plugin to also add support for Groovy (needed for Spock)
apply plugin: 'groovy'

repositories {
    mavenCentral()
}

jar {
    manifest {
        attributes 'Main-Class': 'uk.gov.justice.digital.pdf.Server',
                   'Implementation-Version': version
    }

    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it).matching {
            exclude { it.path.contains('META-INF') } }
        }
    }

    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

dependencies {

    implementation 'ch.qos.logback:logback-classic:1.5.22'
    implementation 'com.google.inject:guice:7.0.0'
    implementation 'com.sparkjava:spark-core:2.9.4'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.20.1'
    implementation 'org.apache.commons:commons-lang3:3.20.0'
    implementation 'com.konghq:unirest-java-core:4.7.0'
    implementation 'commons-io:commons-io:2.21.0'
    implementation 'com.openhtmltopdf:openhtmltopdf-core:1.0.10'
    implementation 'com.openhtmltopdf:openhtmltopdf-pdfbox:1.0.10'
    implementation 'org.jsoup:jsoup:1.21.2'

    compileOnly 'org.projectlombok:lombok:1.18.42'
    annotationProcessor 'org.projectlombok:lombok:1.18.42'

    testImplementation 'org.spockframework:spock-core:2.4-groovy-5.0'
    testImplementation 'org.codehaus.groovy.modules.http-builder:http-builder:0.7.1'
    testImplementation 'org.apache.pdfbox:pdfbox:3.0.6'
}

// Always run tests, because why wouldn't you?
test.outputs.upToDateWhen {false}
