apply plugin: 'java'
java {
    sourceCompatibility = JavaVersion.VERSION_25
    targetCompatibility = JavaVersion.VERSION_25
    toolchain {
        languageVersion = JavaLanguageVersion.of(25)
    }
}

// Apply application plugin for easy application start
apply plugin: 'application'

application {
    mainClass = "uk.gov.justice.digital.pdf.Server"
}

version = '0.1.' + (System.getenv("CIRCLE_BUILD_NUM") ?: "SNAPSHOT")

// Apply the groovy plugin to also add support for Groovy (needed for Spock)
apply plugin: 'groovy'

repositories {
    mavenCentral()
}

jar {
    manifest {
        attributes 'Main-Class': 'uk.gov.justice.digital.pdf.Server',
                'Implementation-Version': version
    }

    from {
        configurations.runtimeClasspath.collect {
            it.isDirectory() ? it : zipTree(it).matching {
                exclude { it.path.contains('META-INF') }
            }
        }
    }

    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

dependencies {

    implementation 'ch.qos.logback:logback-classic:1.5.26'
    implementation 'com.google.inject:guice:7.0.0'
    implementation 'com.sparkjava:spark-core:2.9.4'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.21.0'
    implementation 'org.apache.commons:commons-lang3:3.20.0'
    implementation 'com.konghq:unirest-java-core:4.7.4'
    implementation 'commons-io:commons-io:2.21.0'
    implementation 'com.openhtmltopdf:openhtmltopdf-core:1.0.10'
    implementation 'com.openhtmltopdf:openhtmltopdf-pdfbox:1.0.10'
    implementation 'org.jsoup:jsoup:1.22.1'

    compileOnly 'org.projectlombok:lombok:1.18.42'
    annotationProcessor 'org.projectlombok:lombok:1.18.42'

    testImplementation 'org.spockframework:spock-core:2.4-M7-groovy-5.0'
    testImplementation 'org.apache.groovy:groovy-json'
    testImplementation 'com.konghq:unirest-java-core:4.7.4'
    testImplementation 'com.fasterxml.jackson.core:jackson-databind:2.21.0'

    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

// Always run tests, because why wouldn't you?
test.outputs.upToDateWhen { false }

test {
    useJUnitPlatform()
}

// SparkJava 2.9.4 transitively pulls Jetty 9.4.48 (2022),
// which is NOT compatible with newer JVMs (JDK 21+ / 25).
//
// When running on Java 25, the older Jetty causes the JVM to
// abort during test execution (Gradle Test Executor exit code 100),
// due to illegal / restricted native access.
//
// We explicitly force a newer Jetty 9.4 LTS release here to ensure:
//  - Stable runtime on Java 25
//  - Stable Spock / integration test execution
//  - Consistent behaviour between local, CI and Docker builds
//
// IMPORTANT:
// Removing this block will cause tests to crash on Java 25.
configurations.configureEach {
    resolutionStrategy {
        force "org.eclipse.jetty:jetty-server:12.1.5"
        force "org.eclipse.jetty:jetty-servlet:12.1.5"
        force "org.eclipse.jetty:jetty-util:12.1.5"
        force "org.eclipse.jetty:jetty-http:12.1.5"
        force "org.eclipse.jetty:jetty-io:12.1.5"
        force "org.eclipse.jetty:jetty-webapp:12.1.5"
        force "org.eclipse.jetty.websocket:websocket-server:9.4.58.v20250814"
    }
}

